'''
 SPDX-License-Identifier: Apache-2.0
 Copyright 2019 Western Digital Corporation or its affiliates.
 
 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at
 
 http:www.apache.org/licenses/LICENSE-2.0
 
 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
'''
import os
Import('Env')

out_dir = os.path.join(Env['OUT_DIR_PATH'], 'bsp')
if not os.path.exists(out_dir):
    os.makedirs(out_dir)
    
bsp_base=os.path.join('board', 'hifive-1', 'bsp')

list_files_c=[
   [os.path.join(bsp_base, 'env', 'freedom-e300-hifive1', 'init.c'), os.path.join(out_dir, 'init.o')],
   [os.path.join(bsp_base, 'drivers', 'plic', 'plic_driver.c'), os.path.join(out_dir, 'plic_driver.o')],
]

list_files_a=[
   [os.path.join('board', 'hifive-1', 'start.S'), os.path.join(out_dir, 'start.o')],
]

# compiler flags
c_flags = [] + Env['C_FLAGS']
a_flags = [] + Env['A_FLAGS']

# defines
Env['BSP_DEF'] = ['_HIFIVE']
c_defines = Env['RTOSAL_DEF'] + Env['BSP_DEF'] + Env['PUBLIC_DEF']

# include paths
include_paths = [ 
  os.path.join(Env['ROOT_DIR'], bsp_base, 'include'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'drivers'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'env'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'env', 'freedom-e300-hifive1'),
]

Env['BSP_INC_PATH'] = include_paths

Env['TARGET_LIBS'] += ['wrap-hifive1']

# linker flags
Env['LINKFLAGS'] += ['-T', os.path.join(Env['ROOT_DIR'], 'board', 'hifive-1', Env['EXAMPLE_NAME']+'-link.lds'),'-L'+os.path.join(Env['ROOT_DIR'], 'board', 'hifive-1')]

# c file objects
objects = []
for list_file in list_files_c:
  objects.append(Env.Object(source=os.path.join(Env['ROOT_DIR'], list_file[0]), target=list_file[1], CPPPATH=include_paths, CCFLAGS=c_flags, CPPDEFINES=c_defines))

# asm file objects
for list_file in list_files_a:
  objects.append(Env.Object(source=os.path.join(Env['ROOT_DIR'], list_file[0]), target=list_file[1], CPPPATH=include_paths, CCFLAGS=a_flags, CPPDEFINES=c_defines))

# for libraries
bsp_lib = Env.Library (target=os.path.join(Env['OUT_DIR_PATH'], 'libs', 'bsp.a'), source=objects)

# return the bsp lib
Return('bsp_lib')
