'''
 SPDX-License-Identifier: Apache-2.0
 Copyright 2019 Western Digital Corporation or its affiliates.
 
 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at
 
 http:www.apache.org/licenses/LICENSE-2.0
 
 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
'''
import os
import utils
Import('Env')

out_dir = os.path.join(Env['OUT_DIR_PATH'], 'bsp')
utils.fnCreateFolder(out_dir)
    
bsp_base=os.path.join('board', 'hifive1', 'bsp')

# C language source and out files in list of lists
# [sourcefile.c, outputfile.o]
list_files_c=[
   [os.path.join(bsp_base, 'env', 'freedom-e300-hifive1', 'init.c'), os.path.join(out_dir, 'init.o')],
   [os.path.join(bsp_base, 'drivers', 'plic', 'plic_driver.c'), os.path.join(out_dir, 'plic_driver.o')],
]

# Assembly language source and out files in list of lists
# [sourcefile.S, outputfile.o]
list_files_a=[
   [os.path.join('board', 'hifive1', 'start.S'), os.path.join(out_dir, 'start.o')],
]

# compiler directivs
c_compiler_directives = [] + Env['C_FLAGS']
a_compiler_directives = [] + Env['A_FLAGS']

# defines
Env['PUBLIC_DEF'] += ['D_HI_FIVE1']
c_defines = [ ] + Env['PUBLIC_DEF']

# public includes
Env['PUBLIC_INC'] += [ 
  os.path.join(Env['ROOT_DIR'], bsp_base, 'include', 'sifive'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'include', 'sifive', 'devices'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'env'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'env', 'freedom-e300-hifive1'),
]

# privte includes
include_paths = [ 
  os.path.join(Env['ROOT_DIR'], bsp_base, 'include'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'drivers'),
  os.path.join(Env['ROOT_DIR'], bsp_base, 'drivers', 'plic')
] + Env['PUBLIC_INC']

# add a library to the link command board/hifive1/psb/libwrap-hifive1.a
Env['TARGET_LIBS'] += ['wrap-hifive1']

if not Env["Scan"]:
  # c file objects
  listObjects = []
  for list_file in list_files_c:
    listObjects.append(Env.Object(source=os.path.join(Env['ROOT_DIR'], list_file[0]), target=list_file[1], CPPPATH=include_paths, CCFLAGS=c_compiler_directives, CPPDEFINES=c_defines))

  # asm file objects
  for list_file in list_files_a:
    listObjects.append(Env.Object(source=os.path.join(Env['ROOT_DIR'], list_file[0]), target=list_file[1], CPPPATH=include_paths, CCFLAGS=a_compiler_directives, CPPDEFINES=c_defines))

  # create the bsp library from he objects
  bsp_lib = Env.Library (target=os.path.join(Env['OUT_DIR_PATH'], 'libs', 'bsp.a'), source=listObjects)

  # return the bsp library
  Return('bsp_lib')
